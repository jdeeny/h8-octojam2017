# Chip8 is a virtual machine designed in 1977 for programming video games.
# Octo is a high level assembler, disassembler and simulator for Chip8.
# Click 'Run' and then press ASWD to move the sprite around the screen.
# Click the Octo logo for source, documentation and examples.

:const KB_1 0x1
:const KB_2 0x2
:const KB_3 0x3
:const KB_4 0xC
:const KB_Q 0x4
:const KB_W 0x5
:const KB_E 0x6
:const KB_R 0xD
:const KB_A 0x7
:const KB_S 0x8
:const KB_D 0x9
:const KB_F 0xE
:const KB_Z 0xA
:const KB_X 0x0
:const KB_C 0xB
:const KB_V 0xF

:calc KEY_LEFT { KB_A }
:calc KEY_RIGHT { KB_D }
:calc KEY_UP { KB_W }
:calc KEY_DOWN { KB_S }

# This could be switch instead of a dedicated key for each
:calc KEY_LH { KB_Q }
:calc KEY_RH { KB_E }


:calc MOVE_SPEED { 2 }
:calc MOVE_SPEED_REVERSE { 0 - MOVE_SPEED }

:const FRAME_DELAY 2


# Positions stored here
:alias lhx v3
:alias lhy v4
:alias rhx v5
:alias rhy v6

:alias xtemp v2
:alias ytemp v1

:alias limb v7 # Which limb are we controlling?
:const LIMB_LEFTHAND 0
:const LIMB_RIGHTHAND 1

:macro draw label x y h { i := label sprite x y h }
:macro ifkey keynum { vF := keynum if vF key then }


:macro draw_hand_and_arm_inc_arm exit-label {
	v0 += 16
	vF := 64
	vF &= v0
	if vF != 0 then jump exit-label
}

:macro draw_hand_and_arm x y hand-label arm-label exit-label {
	i := hand-label
	sprite x y 0
	i := arm-label
	v0 := y
	draw_hand_and_arm_inc_arm exit-label
	sprite x v0 0
	draw_hand_and_arm_inc_arm exit-label
	sprite x v0 0
	draw_hand_and_arm_inc_arm exit-label
	sprite x v0 0
	: exit-label
}


:macro update_limb x y hand-sprite arm-sprite exit-a exit-b {
	draw_hand_and_arm x y hand-sprite arm-sprite exit-a
	x += xtemp
	y += ytemp
	draw_hand_and_arm x y hand-sprite arm-sprite exit-b
	xtemp := 0
	ytemp := 0
}


:macro fix_hands_crossing left-x right-x {

}

: main
	hires
	title_draw
	vF := key
	clear

	lhx := 20
	lhy := 58
	rhx := 100
	rhy := 58

	xtemp := 0
	ytemp := 0

	draw_hand_and_arm lhx lhy left_hand arm exit1
	draw_hand_and_arm rhx rhy right_hand arm exit2

	: main_loop
	loop

		if limb == LIMB_LEFTHAND then jump lefthand
		: righthand
			update_limb rhx rhy right_hand arm exit3 exit4
			jump switchlimbs

		: lefthand
			update_limb lhx lhy left_hand arm exit5 exit6

		: switchlimbs
			ifkey KEY_LH limb := LIMB_LEFTHAND
			ifkey KEY_RH limb := LIMB_RIGHTHAND
			ifkey KEY_LEFT xtemp += MOVE_SPEED_REVERSE
			ifkey KEY_RIGHT xtemp += MOVE_SPEED
			ifkey KEY_UP ytemp += MOVE_SPEED_REVERSE
			ifkey KEY_DOWN ytemp += MOVE_SPEED

			v0 := random 0b00001100
			vF := random 0b00000001
			jump0 randtable
			: randtable
			xtemp += vF
			jump endrand
			xtemp -= vF
			jump endrand
			ytemp += vF
			jump endrand
			ytemp -= vF
			: endrand

		: delay_loop
		# lock the framerate of this program via the delay timer:
		loop
			vf := delay
			if vf != 0 then
		again
		vf := FRAME_DELAY
		delay := vf
	again




: right_hand
0x01 0x40 0x06 0xA0 0x02 0xA8 0x0A 0xA8
0x0B 0xF8 0x0F 0xF0 0x0F 0xF0 0x07 0xE0
0x03 0xC0 0x03 0xC0 0x03 0xC0 0x03 0xC0
0x03 0xC0 0x03 0xC0 0x03 0xC0 0x03 0xC0

: left_hand
0x02 0x80 0x05 0x60 0x15 0x40 0x15 0x50
0x1F 0xD0 0x0F 0xF0 0x0F 0xF0 0x07 0xE0
0x03 0xC0 0x03 0xC0 0x03 0xC0 0x03 0xC0
0x03 0xC0 0x03 0xC0 0x03 0xC0 0x03 0xC0

: arm
0x03 0xC0 0x03 0xC0 0x03 0xC0 0x03 0xC0
0x03 0xC0 0x03 0xC0 0x03 0xC0 0x03 0xC0
0x03 0xC0 0x03 0xC0 0x03 0xC0 0x03 0xC0
0x03 0xC0 0x03 0xC0 0x03 0xC0 0x03 0xC0


####################
### Title Screen ###
####################
:const TITLE_CHAR_WIDTH 16

:const TITLE_H8_YOFFSET 17
:const TITLE_H8_SPACING 6
:calc TITLE_H8_XSKIP { TITLE_H8_SPACING + 16 }
:calc TITLE_H8_XOFFSET { 64 - ( ( TITLE_CHAR_WIDTH + TITLE_H8_XSKIP ) / 2 ) }

:calc TITLE_LH_XOFFSET { TITLE_H8_XOFFSET / 3 }
:calc TITLE_LH_YOFFSET { 64 - 6 }
:calc TITLE_RH_XOFFSET { TITLE_H8_XOFFSET + 19 }
:calc TITLE_RH_YOFFSET { TITLE_H8_YOFFSET + 27 }


:const TITLE_SUB_WIDTH 8
:const TITLE_SUB_HEIGHT 5
:const TITLE_SUB_TILES 10
:calc TITLE_SUB_TOTAL_WIDTH { ( TITLE_SUB_WIDTH * TITLE_SUB_TILES ) - 3 } # Subtract for empty cols at end
:calc TITLE_SUB_XOFFSET { 64 - ( TITLE_SUB_TOTAL_WIDTH / 2 ) }
:calc TITLE_SUB_YOFFSET { TITLE_H8_YOFFSET - 10 }
:calc TITLE_SUB_XEND { TITLE_SUB_XOFFSET + ( TITLE_SUB_WIDTH * TITLE_SUB_TILES ) }

: title_draw
	clear
	v0 := TITLE_H8_XOFFSET
	v1 := TITLE_H8_YOFFSET
	draw title_htop v0 v1 0
	v1 += 16
	draw title_hbot v0 v1 0
	v0 += TITLE_H8_XSKIP
	draw title_8bot v0 v1 0
	v1 += -16
	draw title_8top v0 v1 0
	v1 := TITLE_LH_XOFFSET
	v2 := TITLE_LH_YOFFSET
	draw_hand_and_arm v1 v2 left_hand arm exit_title1
	v1 := TITLE_RH_XOFFSET
	v2 := TITLE_RH_YOFFSET
	draw_hand_and_arm v1 v2 right_hand arm exit_title2


	v1 := TITLE_SUB_XOFFSET
	v2 := TITLE_SUB_YOFFSET
	v0 := TITLE_SUB_HEIGHT
	i := title_sub_be
	loop
		sprite v1 v2 TITLE_SUB_HEIGHT
		v1 += TITLE_SUB_WIDTH
		i += v0
		while v1 != TITLE_SUB_XEND
	again
	;

: title_htop
0x00 0x00 0x60 0x06 0xF0 0x0F 0xF0 0x0F
0xF0 0x0F 0xF0 0x0F 0xF0 0x0F 0xF0 0x0F
0xF0 0x0F 0xF0 0x0F 0xF0 0x0F 0xF0 0x0F
0xF0 0x0F 0xF0 0x0F 0xFF 0xFF 0xFF 0xFF

: title_hbot
0xFF 0xFF 0xFF 0xFF 0xF0 0x0F 0xF0 0x0F
0xF0 0x0F 0xF0 0x0F 0xF0 0x0F 0xF0 0x0F
0xF0 0x0F 0xF0 0x0F 0xF0 0x0F 0xF0 0x0F
0xF0 0x0F 0xF0 0x0F 0xF0 0x0F 0x60 0x06

: title_8top
0x30 0x0C 0x78 0x1E 0x78 0x1E 0x30 0x0C
0x07 0xE0 0x1F 0xF8 0x3F 0xFC 0x7F 0xFE
0x7C 0x3E 0xF8 0x1F 0xF0 0x0F 0xF0 0x0F
0xF0 0x0F 0xF0 0x0F 0xF8 0x1F 0x7C 0x3E


: title_8bot
0x3F 0xFC 0x1F 0xF8 0x1F 0xF8 0x3F 0xFC
0x7C 0x3E 0xF8 0x1F 0xF0 0x0F 0xF0 0x0F
0xF0 0x0F 0xF0 0x0F 0xF8 0x1F 0x7C 0x3E
0x7F 0xFE 0x3F 0xFC 0x1F 0xF8 0x07 0xE0


# H8 - Bergsteigersimulator
: title_sub_be	0xCE 0xA8 0xCC 0xA8 0xCE # BE
: title_sub_rg	0xCE 0xA8 0xCA 0xAA 0xAE # RG
: title_sub_st 	0xEE 0x84 0xE4 0x24 0xE4 # ST
: title_sub_ei 	0xEB 0x8A 0xCA 0x8A 0xEB # EI 1/2 G
: title_sub_ge 	0xBB 0x22 0xB3 0xA2 0xBA # GE 2 col R
: title_sub_rsi 0xBA 0xA2 0x3A 0x8A 0xBA # 1 col R,SI
: title_sub_m 	0x8A 0xDA 0xAA 0x8A 0x8B # M 1/2 U
: title_sub_ula 0xA1 0xA2 0xA3 0xA2 0xBA # 1/2 u L 1/2A
: title_sub_TO	0x3B 0x92 0x92 0x92 0x93 # 1/2 A, T, 1/2 O
: title_sub_R 	0xB8 0xA8 0xB0 0xA8 0xA8 # 1/2 O, R



### End Title Screen
